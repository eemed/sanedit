@whitespaced
rules = ws rule* ws;

ws = (_ comment _)*;

WHITESPACE = [ \t\r\n];
_ = WHITESPACE*;

nl = "\r\n" / "\r" / "\n";

@show
comment = "#" (!nl .)* nl;

@whitespaced
rule = ws (_ annotation)* rule_name "=" def+ ";" ws ;

@show(identifier)
@whitespaced
rule_name = id_chars+ &"=";

@show(constant)
annotation = "@" [a..zA..Z_-]+ ("(" [a..zA..Z_-]+ ")")?;

@whitespaced
def = clause (op? clause)*;

@show(operator)
op = "/";

@whitespaced
clause = preop? (parens / brackets / id_chars+ / string) postop?;

@whitespaced
parens = "(" def ")";

@whitespaced
brackets = "[" ("\\" escape_char / [^\]])* "]";

@show
string          = "\"" ("\\" escape_char / [^"])* "\"";
escape_char     = "0" / "t" / "n" / "r" / "\"" / "\\";
hex = "\\x" hex_digit+;
hex_digit = [a..fA..F0..9];

@show(operator)
postop = "?" / "*" / "+";

@show(operator)
preop = "!" / "&";

id_chars = [a..zA..Z_-];
