#
# Annotations:
#    @show        show the rule in the generated AST
#    @whitespaced convert the rule to a form that allows whitespace in between every element.
#                 a special WHITESPACE rule defines what is considered whitespace.
#                 Example:
#                     Original:   array = "["             value (              ","            value )*             "]";
#                     Generated:  array = "[" WHITESPACE* value ( WHITESPACE* "," WHITESPACE* value )* WHITESPACE* "]";
#
document = _ (_ (member / value / .))* _;

WHITESPACE = [ \t\r\n];
_ = WHITESPACE*;


value =  object / array / number / string / constant;

@show
@whitespaced
object = "{" member ("," member)* "}";

@show
@whitespaced
member = identifier ":" value;

identifier = quote identifier_inner quote;

@show(identifier)
identifier_inner = ([^\u0..\u19\u22\u5c])*;

string = quote string_inner quote;

@show(string)
string_inner = ([^\u0..\u19\u22\u5c])*;

@show
@whitespaced
array = "[" value ("," value )* "]";

@show
number = "-"? integer ("." [0..9]+)?;

@show
constant = true / false / null;

true = "true";
false = "false";
null = "null";
integer = "0" / [1..9] [0..9]*;
quote = "\"";
