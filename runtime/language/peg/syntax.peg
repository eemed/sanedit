@whitespaced
rules = ws (rule / .)* ws;
ws = (_ comment _)*;
WHITESPACE = [ \t\r\n];
_ = WHITESPACE*;

nl = "\r\n" / "\r" / "\n";

@show @highlight
comment = "#" (!nl .)* nl;

@whitespaced
rule = ws (_ annotation)* rule_name "=" def+ ";" ws ;

@show @completion(rule) @highlight(identifier) @whitespaced
rule_name = id_chars+;


@show @highlight(preproc)
annotation = "@" annotation_name ("(" ann_spec ")")?;
ann_spec = [a..zA..Z_-]+;

@completion(annotation) @highlight
annotation_name = [a..zA..Z_-]+;

@whitespaced
def = clause (op? clause)*;

@show @highlight(operator)
op = "/";

@whitespaced
clause = preop? (parens / brackets / id_chars+ / string / dot) postop?;

dot = ".";

@whitespaced
parens = "(" def ")";

@whitespaced
brackets = "[" negate? ("\\" escape_char / [^\]])* "]";

@show(operator)
negate = "^";


@show @highlight
string          = "\"" ("\\" escape_char / [^"])* "\"";
escape_char     = "0" / "t" / "n" / "r" / "\"" / "\\" / "]";
hex = "\\x" hex_digit+;
hex_digit = [a..fA..F0..9];

@show @highlight(operator)
postop = "?" / "*" / "+";

@show @highlight(operator)
preop = "!" / "&";

id_chars = [a..zA..Z0..9_-];
