document = (separated / (!stop .)+ / .)*;
separated = todo / keyword / string / constant / comment / preproc / type / operator / alpha;

alpha = [a..zA..Z0..9_]+;
stop = ws+ / nl / "[" / "]" / "(" / ")" / ";" / "<" / ">" / "," / ":" / ".";
WHITESPACE = [ \t] / nl;
nl = "\n" / "\r\n";
ws = WHITESPACE;

@show @highlight
string = char / sstring;
char = "'" . "'";
sstring = "\"" (escape  / [^"])* "\"";
escape = "\\" .;

@show @highlight
constant = ("true" / "false" / "null" / number / typecaps) &stop;
number = (hex / octal / binary / int) num_suffix?;
int = "-"? digit+ ("." (digit / "_")+)? (("e" / "E") ("-" / "+")? digit+)? (("p" / "P") ("+" / "-")? digit+)? "i"?;
hex = "-"? "0" ("x" / "X") (hexdigit / "_")+;
octal = "-"? "0" ("o" / "O") (digit / "_")+;
binary = "-"? "0" ("b" / "B") ("0" / "1" / "_")+;
digit = [0..9];
hexdigit = digit / [a..fA..F];
@show @highlight(operator)
num_suffix = ("f" / "F" / "l" / "L" / "d" / "D");

typecaps = [A..Z0..9_]+;

@show @highlight
comment = line_comment / block_comment;
line_comment = "//" (todo / (!nl .))* nl;
block_comment = "/*" (todo / (!"*/" .))* "*/";

preproc = annotation / preproc_words;
@show @highlight(preproc)
preproc_words = ("import" / "package") &stop;
@show @highlight(preproc)
annotation = "@" [A..Za..z0..9_]+;

@show @highlight
type = ("boolean" / "char" / "void" / "byte" / number_types / classname) &stop;
number_types = "int" / "float" / "double" / "long" / "short";
classname = [A..Z] [a..zA..Z0..9_]*;

@show @highlight
operator = [\[\]{}():,.<>&|=+*/?!<>-];

@show @highlight(warn)
todo = "TODO" / "XXX";

@show @highlight
keyword = (
    "while" /
    "volatile" /
    "var" /
    "try" /
    "transient" /
    "throws" /
    "throw" /
    "this" /
    "synchronized" /
    "switch" /
    "super" /
    "strictfp" /
    "static" /
    "return" /
    "requires" /
    "public" /
    "protected" /
    "private" /
    "new" /
    "native" /
    "module" /
    "interface" /
    "instanceof" /
    "implements" /
    "if" /
    "goto" /
    "for" /
    "finally" /
    "final" /
    "extends" /
    "exports" /
    "enum" /
    "else" /
    "do" /
    "default" /
    "continue" /
    "const" /
    "class" /
    "catch" /
    "case" /
    "break" /
    "assert" /
    "abstract"
    ) &stop;
