document = (special / list / line_comment)? (separated / ws+ / .)*;
separated = blocks / keyword / preproc / comment / operator / string / specials / list_nl / identifier;
WHITESPACE = [ \t];
ws = WHITESPACE;
nl = "\n" / "\r\n";
stop = WHITESPACE+ / nl;

list_nl = nl ws* list ws;
@show @highlight(operator)
list = "*"+;

preproc = special_nl / rectangle / angle / tags / link;
special_nl = nl special;
@show @highlight(preproc)
special = ":" (!(":" / nl) .)* ":" &stop;
@show @highlight(preproc)
rectangle = "["+ (!"]" .)* "]"+;
@show @highlight(preproc)
angle = "<<" (!">>" .)* ">>";
@show @highlight(preproc)
link = "<" (!">" .)* ">";
@show @highlight(preproc)
tags = [a..zA..Z0..9.]+ ":" ":"? [a..zA..Z0..9./_-]* "[" (!"]" .)* "]";

blocks = source / example / literal / pass / quote;

@show @highlight(preproc)
source_inj = "[" (source_inj_lang &"]" / (!(nl / "]" / ",") .)* ",")+ "]";
@injection-language
source_inj_lang = (!("," / "]" / nl) .)+;
source =  source_inj nl sourceop nl sourcec sourceop / sourceop nl sourcec2 sourceop ;
@show @inject
sourcec = (!sourceop .)*;
@show @highlight(string)
sourcec2 = (!sourceop .)*;
@show @highlight(operator)
sourceop = "----";

pass = passop nl pass_content passop;
@show @highlight(string)
pass_content = (!passop .)*;
@show @highlight(operator)
passop = "++++";

example = exampleop nl examplec exampleop;
@show @highlight(string)
examplec = (!exampleop .)*;
@show @highlight(operator)
exampleop = "====";

# sidebar = sidebarop nl sidebarc sidebarop;
# @show @highlight(string)
# sidebarc = (!sidebarop (separated / .))*;
# @show @highlight(operator)
# sidebarop = "****";

literal = litop &nl litc litop;
@show @highlight(string)
litc = (!litop .)*;
@show @highlight(operator)
litop = "....";

quote = qop &nl qc qop;
@show @highlight(operator)
qop = "____";
@show @highlight(string)
qc = (!qop (keyword / .))*;


@show @highlight
identifier = title / description / preindent;
title = "=" (!nl .)*;
description = nl "." &[a..zA..Z0..9] (!nl .)*;
preindent = nl ws+ (!nl .)* nl;

operator = ops / table;
@show @highlight(operator)
ops = ("*"+ / "."+ / "-"+ / "+"+ / "'''") &stop;
table = table_border (!table_border table_cell nl?)+ table_border;
@show @highlight(operator)
table_border = "|==" "="* nl;
@show @highlight(operator)
table_cell_border = "|";
table_cell = table_cell_border (!(table_cell_border / nl) .)*;

comment = block_comment / nl line_comment;
@show @highlight(comment)
line_comment = "//" (!nl .)*;
@show @highlight(comment)
block_comment = "////" (!"////" .)*  "////";

@show @highlight
string    = "\"" nstring "\"" / "'" sstring "'" / "`" tstring "`" ;
nstring    = ("\\" escape_char / [^"\n])*;
sstring    = ("\\" escape_char / [^'\n])*;
tstring    = ("\\" escape_char / [^`\n])*;
escape_char     = "0" / "t" / "n" / "r" / "'" / "`" / "\"" / "\\";

# These are found in text
keyword = (text (letter / bold / subscript / superscript / force / emphtwo / emph / escape))+;
text = [a..zA..Z0..9-]*;
@show @highlight(keyword)
specials = ("NOTE" / "WARNING" / "IMPORTANT" / "TIP" / "CAUTION") &":";

@show @highlight(keyword)
bold = constrained_start "*" (!(nl / "*") ( escape / .))+ "*" constrained_end;
@show @highlight(keyword)
letter = "**" (!(nl / "**") ( escape / .))+ "**";
@show @highlight(keyword)
subscript = "~" (!(nl / ws / "~") ( escape / .))+ "~";
@show @highlight(keyword)
superscript = "^" (!(nl / ws / "^") ( escape / . ))+ "^";
@show @highlight(keyword)
force = "{" (!(nl / "}") ( escape / .))+ "}";
@show @highlight(keyword)
emphtwo = "__" (!(nl / "__") ( escape / .))+ "__";
@show @highlight(keyword)
emph = constrained_start "_" (!(nl / "_") ( escape / .))+ "_" constrained_end;

constrained_start = (ws / nl);
constrained_end = &[^a..zA..Z];

@show @highlight(keyword)
escape = "\\" .;
