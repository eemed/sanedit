#
# Annotations:
#    @show        show the rule in the generated AST
#    @whitespaced convert the rule to a form that allows whitespace in between every element.
#                 a special WHITESPACE rule defines what is considered whitespace.
#                 Example:
#                     Original:   array = "["             value (              ","            value )*             "]";
#                     Generated:  array = "[" WHITESPACE* value ( WHITESPACE* "," WHITESPACE* value )* WHITESPACE* "]";
#
document = (!skip .)* (member / value / .)*;

WHITESPACE = [ \t\r\n];
_ = WHITESPACE*;

skip = "{" / "[";
value =  object / array / number / string / constant;

@whitespaced
object = "{" (member ("," member)*)? "}";

@whitespaced
member = identifier ":" value;

@show @highlight
identifier = quote identifier_inner quote;

@show @completion
identifier_inner = ([^\u{0}..\u{19}\u{22}\u{5c}])*;

@show @highlight
string = quote string_inner quote;

@show @completion @highlight(string)
string_inner = ([^\u{0}..\u{19}\u{22}\u{5c}])*;

@whitespaced
array = "[" value ("," value )* "]";

@show @highlight(constant)
number = "-"? integer ("." [0..9]+)?;

@show @highlight
constant = true / false / null;

true = "true";
false = "false";
null = "null";
integer = "0" / [1..9] [0..9]*;
quote = "\"";
