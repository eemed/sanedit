#
# Annotations:
#    @show        show the rule in the generated AST
#    @whitespaced convert the rule to a form that allows whitespace in between every element.
#                 a special WHITESPACE rule defines what is considered whitespace.
#                 Example:
#                     Original:   array = "["             value (              ","            value )*             "]";
#                     Generated:  array = "[" WHITESPACE* value ( WHITESPACE* "," WHITESPACE* value )* WHITESPACE* "]";
#
document = _ (_ (member / value / .))* _;

WHITESPACE = [ \t\r\n];
_ = WHITESPACE*;


value =  object / array / number / string / constant;

@show @whitespaced @highlight
object = "{" member ("," member)* "}";

@show @whitespaced @highlight
member = identifier ":" value;

@show
identifier = quote identifier_inner quote;

@show @completion @highlight(identifier)
identifier_inner = ([^\u0..\u19\u22\u5c])*;

@show @highlight
string = quote string_inner quote;

@show @completion @highlight(string)
string_inner = ([^\u0..\u19\u22\u5c])*;

@show @whitespaced @highlight
array = "[" value ("," value )* "]";

@show @highlight
number = "-"? integer ("." [0..9]+)?;

@show @highlight
constant = true / false / null;

true = "true";
false = "false";
null = "null";
integer = "0" / [1..9] [0..9]*;
quote = "\"";
